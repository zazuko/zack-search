PREFIX : <http://voc.zazuko.com/zack#>
PREFIX  rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX  text: <http://jena.apache.org/text#>
PREFIX  rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX  skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX  xsd: <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT {
  :query :result ?sub .
  ?sub a :resultItem .
  ?sub ?pred ?obj .
  ?sub1 <http://data.archiveshub.ac.uk/def/level> ?level1 .
  ?sub1 <http://purl.org/dc/elements/1.1/title> ?title1 .
  ?sub1 <http://purl.org/dc/elements/1.1/relation> ?sub2 .
  ?sub2 <http://data.archiveshub.ac.uk/def/level> ?level2 .
  ?sub2 <http://purl.org/dc/elements/1.1/title> ?title2 .
  ?sub2 <http://purl.org/dc/elements/1.1/relation> ?sub3 .
  ?sub3 <http://data.archiveshub.ac.uk/def/level> ?level3 .
  ?sub3 <http://purl.org/dc/elements/1.1/title> ?title3 .
  ?sub3 <http://purl.org/dc/elements/1.1/relation> ?sub4 .
  ?sub4 <http://data.archiveshub.ac.uk/def/level> ?level4 .
  ?sub4 <http://purl.org/dc/elements/1.1/title> ?title4 .
  ?sub :score ?totalScore .
} WHERE {
  {
    SELECT ?sub ?totalScore WHERE {
      ${textmatch}
      ${filters}

      OPTIONAL {
        ?sub <http://www.w3.org/2006/time#intervalStarts> ?startOptional .
      }
      BIND ( COALESCE(STR(?startOptional), "0001-01-01") AS ?start )
      BIND ( xsd:date(CONCAT(SUBSTR(?start, 0, 10), SUBSTR("0001-01-01", STRLEN(?start) + 1))) AS ?startDate )
      BIND ( xsd:date(NOW()) - ?startDate AS ?startDelta )
      BIND ( COALESCE(xsd:float(STRBEFORE(STRAFTER(STR(?startDelta),"P"), "D")), 0) AS ?startScore )
      BIND ( COALESCE(?score, ?startScore) AS ?totalScore )
    }
    ORDER BY DESC(?totalScore)
    OFFSET ${offset}
    LIMIT ${limit}
  }
  GRAPH ?g {
    ?sub ?pred ?obj .

    OPTIONAL {
      ?sub <http://purl.org/dc/elements/1.1/relation> ?sub1 .
      ?sub1 <http://data.archiveshub.ac.uk/def/level> ?level1 .
	  ?sub1 <http://purl.org/dc/elements/1.1/title> ?title1 .
	  OPTIONAL {
	    ?sub1 <http://purl.org/dc/elements/1.1/relation> ?sub2 .
        ?sub2 <http://data.archiveshub.ac.uk/def/level> ?level2 .
      	?sub2 <http://purl.org/dc/elements/1.1/title> ?title2 .
      	OPTIONAL {
          ?sub2 <http://purl.org/dc/elements/1.1/relation> ?sub3 .
          ?sub3 <http://data.archiveshub.ac.uk/def/level> ?level3 .
          ?sub3 <http://purl.org/dc/elements/1.1/title> ?title3 .
          OPTIONAL {
            ?sub3 <http://purl.org/dc/elements/1.1/relation> ?sub4 .
            ?sub4 <http://data.archiveshub.ac.uk/def/level> ?level4 .
            ?sub4 <http://purl.org/dc/elements/1.1/title> ?title4 .
          }
        }
	  }
	}
  }
}
